plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

group 'net.neoforged.gradleutils'
version '3.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
}

task copyGradleUtilsGroovy(type: Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/groovy'
    from('buildSrc/src/main/groovy') {
        include '**/*.groovy'
        include '**/*.java'
    }
}

task copyGradleUtilsResources(type: Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/resources'
    from('buildSrc/build/extraresources') {
        include '**/*.zip'
    }
}

compileGroovy.dependsOn(copyGradleUtilsResources)
checkLicenseMain.dependsOn(copyGradleUtilsResources)
compileGroovy.dependsOn(copyGradleUtilsGroovy)
checkLicenseMain.dependsOn(copyGradleUtilsGroovy)

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

license {
    header = file('HEADER')
    ext {
        name = 'GradleUtils'
        year = 2021
        fullname = 'Forge Development LLC'
    }
    exclude '**/*.properties'
}

gradlePlugin {
    plugins {
        gradleutils {
            id = 'net.neoforged.gradleutils'
            implementationClass = 'net.minecraftforge.gradleutils.GradleUtilsPlugin'
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) { mavenPublication ->
            artifact sourcesJar

            pom {
                name = 'Gradle Utils'
                description = 'Used by MinecraftForge projects as a util library for Gradle buildscripts'
                url = 'https://github.com/NeoForged/GradleUtils'
                scm {
                    url = 'https://github.com/NeoForged/GradleUtils'
                    connection = 'scm:git:git://github.com/NeoForged/GradleUtils.git'
                    developerConnection = 'scm:git:git@github.com:NeoForged/GradleUtils.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/NeoForged/GradleUtils/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                    developer {
                        id = 'SizableShrimp'
                        name = 'SizableShrimp'
                    }
                    developer {
                        id = 'marchermans'
                        name = 'Orion'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'forge'
            if (System.env.MAVEN_USER && System.env.MAVEN_PASSWORD) {
                def publishingEndpoint = System.env.MAVEN_URL_RELEASE ?: 'https://maven.neoforged.net/releases'

                if (project.version.toString().endsWith("-SNAPSHOT") && System.env.MAVEN_URL_SNAPSHOTS) {
                    url System.env.MAVEN_URL_SNAPSHOTS
                } else {
                    url publishingEndpoint
                }
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = System.env.MAVEN_USER
                    password = System.env.MAVEN_PASSWORD
                }
            } else {
                url 'repo'
            }
        }
    }
}
