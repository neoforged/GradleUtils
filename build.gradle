plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
}

group 'net.neoforged.gradleutils'
version '3.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    api 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
}

sourceSets {
    teamcityTemplate
}

def teamcityTemplateZip = tasks.register('createTeamcityTemplateZip', Zip) {
    from(sourceSets.teamcityTemplate.output)
    archiveFileName.set('.teamcity.zip')
    destinationDirectory.set(layout.buildDirectory.dir('template'))
}

tasks.named(sourceSets.main.processResourcesTaskName, Copy) {
    from(teamcityTemplateZip)
}

license {
    header(file('HEADER'))
    skipExistingHeaders = true // Ignore existing license headers on files
}

// Disable the license tasks for the TeamCity template files
['checkLicenseTeamcityTemplate', 'updateLicenseTeamcityTemplate'].each {
    tasks.named(it) {
        enabled = false
    }
}

gradlePlugin {
    plugins {
        gradleutils {
            id = 'net.neoforged.gradleutils'
            implementationClass = 'net.neoforged.gradleutils.GradleUtilsPlugin'
        }
    }
}

final changelogTask = tasks.register('changelog') {
    RegularFileProperty outputFile = objects.fileProperty().convention(layout.buildDirectory.file('changelog.txt'))
    it.outputs.file(outputFile).withPropertyName('output changelog file')

    it.description = 'Generates a commit changelog using the git CLI command'
    it.doLast {
        try {
            outputFile.get().asFile.withOutputStream { output ->
                project.exec { ExecSpec spec ->
                    // TODO: limit to last reachable tag?
                    spec.commandLine('git', 'log', '--no-show-signature', '--no-color',
                            '--pretty=%h% (describe:tags=true:abbrev) (%aN) %s')
                            .standardOutput(output)
                }.assertNormalExitValue().rethrowFailure()
            }
        } catch (Exception e) {
            throw new IllegalStateException("Failed to generate changelog; check if 'git' is on the PATH", e)
        }
    }
}

tasks.named('assemble') {
    it.dependsOn changelogTask
}

publishing {
    publications {
        pluginMaven(MavenPublication) { mavenPublication ->
            artifact(changelogTask) {
                classifier = 'changelog'
                extension = 'txt'
            }
            pom {
                name = 'Gradle Utils'
                description = 'Gradle buildscript utility library for NeoForged projects'
                url = 'https://github.com/neoforged/GradleUtils'
                scm {
                    url = 'https://github.com/neoforged/GradleUtils'
                    connection = 'https://github.com/neoforged/GradleUtils.git'
                    developerConnection = 'scm:git:git@github.com:NeoForged/GradleUtils.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/neoforged/GradleUtils/issues'
                }
                organization {
                    name = 'NeoForged'
                    url = 'https://github.com/neoforged'
                }
                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'forge'
            if (System.env.MAVEN_USER && System.env.MAVEN_PASSWORD) {
                def publishingEndpoint = System.env.MAVEN_URL_RELEASE ?: 'https://maven.neoforged.net/releases'

                if (project.version.toString().endsWith("-SNAPSHOT") && System.env.MAVEN_URL_SNAPSHOTS) {
                    url System.env.MAVEN_URL_SNAPSHOTS
                } else {
                    url publishingEndpoint
                }
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = System.env.MAVEN_USER
                    password = System.env.MAVEN_PASSWORD
                }
            } else {
                url 'repo'
            }
        }
    }
}

tasks.register("configureTeamCity") {
    // Print marker lines into the log which configure the pipeline
    doLast {
        project.getLogger().lifecycle("Setting project variables and parameters.")
        println "##teamcity[buildNumber '${project.version}']"
        println "##teamcity[setParameter name='env.PUBLISHED_JAVA_ARTIFACT_VERSION' value='${project.version}']"
    }
}
